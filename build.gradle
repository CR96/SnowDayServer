buildscript {
	ext {
		springBootVersion = '2.0.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id "com.moowork.node" version "1.2.0"
}

node {
	version = '10.6.0'
	distBaseUrl = 'https://nodejs.org/dist'
	download = true
	workDir = file("${project.buildDir}/nodejs")
	yarnWorkDir = file("${project.buildDir}/yarn")
	nodeModulesDir = file("src/main/react")
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'

war {
	archiveName 'gbsnowday.war'
}

group = 'com.gbsnowday'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

configurations {
	providedRuntime
}

task runBuild(type: YarnTask) {
	args = ['run', 'build']
}

task copyTask(type: Copy) {
	from 'src/main/react/build/static/'
	into 'src/main/resources/static/'
}

clean.doFirst{
    delete 'src/main/resources/static/'
}

clean.finalizedBy(yarn_install)
yarn_install.finalizedBy(runBuild)
runBuild.finalizedBy(copyTask)

dependencies {
	compile('com.google.code.gson:gson:2.8.4')
	compile('org.jsoup:jsoup:1.11.3')

	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-thymeleaf')
	compileOnly('org.projectlombok:lombok')
	providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}
